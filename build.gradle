plugins {
    id("maven-publish")
    id("java-library")
    id("gronk").version("latest.release")
    id("me.champeau.jmh").version("latest.release")
}

group("net.auoeke")
version("5.1.0")

javaVersion(17)

sourceSets {
    jmh {
        java.srcDirs = ["benchmark"]
    }
}

configurations {
    [jmhAnnotationProcessor, testAnnotationProcessor]*.tap {
        extendsFrom(annotationProcessor)
    }
}

gronk {
    uncheck()
}

dependencies {
    annotationProcessor(project(":lib"), project(path: ":lib", configuration: "shadow"))
    api("net.auoeke:unsafe")

    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("org.ow2.asm:asm-tree")
    testImplementation("org.openjdk.jol:jol-core")
}

compileTestJava {
    mustRunAfter(jar)

    options.release.set(JavaVersion.current().majorVersion as int)
    options.compilerArgs << "--enable-preview"
}

jmh {
    fork = 1
    warmup = "500ms"
    timeOnIteration = "300ms"
}

test {
    dependsOn(cleanTest)

    useJUnitPlatform()
    systemProperty("junit.jupiter.testclass.order.default", 'org.junit.jupiter.api.ClassOrderer$OrderAnnotation')
    jvmArgs("-XX:+IgnoreUnrecognizedVMOptions", "-Xdump:none", "-XX:ErrorFile=${systemProperty("java.io.tmpdir")}/java_err_%p.log")
}

jar {
    manifest.from("resources/META-INF/MANIFEST.MF")
}

// For Reflect::instrumentation.
task testRelease(type: Test) {
    group(LifecycleBasePlugin.VERIFICATION_GROUP)
    dependsOn(cleanTestRelease, jar)
    check.dependsOn(it)

    useJUnitPlatform()
    systemProperty("junit.jupiter.testclass.order.default", 'org.junit.jupiter.api.ClassOrderer$OrderAnnotation')
    classpath = classpath - sourceSets.main.output + files(jar.archiveFile)
}

publishing {
    repositories {
        mavenLocal()

        maven("maven.auoeke.net") {
            username(systemProperty("maven.username"))
            password(systemProperty("maven.password"))
        }
    }
}
